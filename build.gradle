buildscript {
    ext {
        spring = '2.7.9'
        boot = "org.springframework.boot"
        lombok = "org.projectlombok:lombok"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("$boot:spring-boot-gradle-plugin:$spring")
    }
}

allprojects {
    group = 'com.toppings'
    version = '0.0.1'
}

subprojects {
    apply plugin: 'java'
    apply plugin: boot
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    configurations {
        runtimeClasspath {
            extendsFrom developmentOnly
        }

        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
        systemProperty 'jasypt.encryptor.password', project.findProperty("jasypt.encryptor.password")

        reports {
            junitXml.enabled(true)
        }

        finalizedBy jacocoTestReport
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        compileOnly 'org.projectlombok:lombok'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        annotationProcessor 'org.projectlombok:lombok'
        runtimeOnly 'com.mysql:mysql-connector-j'
        testCompileOnly 'org.projectlombok:lombok' // 테스트 의존성 추가
        testAnnotationProcessor 'org.projectlombok:lombok' // 테스트 의존성 추가
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    tasks.register("prepareKotlinBuildScriptModel"){}

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            csv.required = false
            html.required = false
        }

        afterEvaluate {
            classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, excludes: [
                        '**/*Application*',
                        '**/*Exception*',
                        '**/dto/**',
                        '**/config/**',
                    ])
                })
            )
        }
    }
}
